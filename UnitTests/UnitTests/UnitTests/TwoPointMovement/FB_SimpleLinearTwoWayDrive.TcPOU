<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_SimpleLinearTwoWayDrive" Id="{d5079cf3-2029-4674-b5d0-714b53c5075a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SimpleLinearTwoWayDrive EXTENDS FB_Unit
VAR_INPUT
	SensorHomePos: BOOL;
	SensorWorkPos: BOOL;
	Enable: BOOL;
	LockHomePos: BOOL;
	LockWorkPos: BOOL;
	HoldPosition: BOOL;	// TRUE: Hold output to true even if position has been reached
	ToHome: BOOL;
	ToWork: BOOL;
END_VAR
VAR_OUTPUT	
	ActorHomePos: BOOL;
	ActorWorkPos: BOOL;
END_VAR
VAR
END_VAR
VAR PERSISTENT
	stHMI: ST_SimpleLinearTwoWayDriveHmi;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// This could be refactored further by adding a data structure instead of inputs.
// advantages:
// - could be transferred to HMI
// - further removal of code dublication
// disadvantages:
// - adds another layer of indirection, e.g. for cross-reference search

CASE SUPER^.OpMode OF
	E_OpMode.Manual, E_OpMode.Jog:
		ActorHomePos := SetOutput(stHmi.ToHomePos.HmiToPlc, SensorHomePos, LockHomePos, ActorWorkPos);
		ActorWorkPos := SetOutput(stHmi.ToWorkPos.HmiToPlc, SensorWorkPos, LockWorkPos, ActorHomePos);	
	E_OpMode.AutomaticRunning:
		ActorHomePos := SetOutput(ToHome, SensorHomePos, LockHomePos, ActorWorkPos);
		ActorWorkPos := SetOutput(ToWork, SensorWorkPos, LockWorkPos, ActorHomePos);	
	ELSE
		ActorHomePos := ActorWorkPos := FALSE; 
END_CASE 
]]></ST>
    </Implementation>
    <Method Name="SetOutput" Id="{2b2cbc49-cd42-42ab-8144-70652882f6fc}">
      <Declaration><![CDATA[METHOD PRIVATE SetOutput : BOOL
VAR_INPUT
	MoveToPos : BOOL;
	PosReached: BOOL;
	DirectionLocked: BOOL;
	OutputInterlock: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetOutput := MoveToPos AND Enable AND NOT DirectionLocked AND NOT OutputInterlock AND (NOT PosReached OR HoldPosition);]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_SimpleLinearTwoWayDrive">
      <LineId Id="36" Count="5" />
      <LineId Id="69" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="79" Count="1" />
      <LineId Id="72" Count="0" />
      <LineId Id="77" Count="1" />
      <LineId Id="74" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="63" Count="0" />
    </LineIds>
    <LineIds Name="FB_SimpleLinearTwoWayDrive.SetOutput">
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>